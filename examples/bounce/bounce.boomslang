
# Bouncing Square
# (c) 2016 Isaac Shelton

import glfw

def main()
    unless glfwInit()
        die()
    
    GlfwMonitor^ monitor = glfwGetPrimaryMonitor()
    GlfwVideoMode videomode = glfwGetVideoMode(monitor)
    GlfwWindow^ window = glfwCreateWindow(videomode.width.toDouble(), videomode.height.toDouble(), "Bouncing Square", monitor, void)
    
    final Double xspeed = 4
    Double xposition = 0
    Double xdirection = 1
    
    final Double yspeed = 4
    Double yposition = 0
    Double ydirection = 1
    
    if window = void
        "Failed to init window".output()
        die()
    
    glfwMakeContextCurrent(window)
    glfwSwapInterval(1)
    
    glClearColor(1, 1, 1, 1)
    
    until glfwWindowShouldClose(window)
        glClear(GL_COLOR_BUFFER_BIT)
		
        glMatrixMode(GL_PROJECTION)
        glLoadIdentity()
		glViewport(0i, 0i, videomode.width, videomode.height)
        glOrtho(0, videomode.width.toDouble(), videomode.height.toDouble(), 0, 1, -1)
        
        glColor3f(1, 0, 0)
        glBegin(GL_QUADS)
          glVertex2f(0 + xposition,   0 + yposition)
          glVertex2f(0 + xposition,   100 + yposition)
          glVertex2f(100 + xposition, 100 + yposition)
          glVertex2f(100 + xposition, 0 + yposition)
        glEnd()
        
        if xposition >= videomode.width.toDouble() - 100
            xdirection = -1
        else if xposition <= 0
            xdirection = 1
        
        if yposition >= videomode.height.toDouble() - 100
            ydirection = -1
        else if yposition <= 0
            ydirection = 1
        
        xposition += xspeed * xdirection
        yposition += yspeed * ydirection
        
        glfwPollEvents()
        glfwSwapBuffers(window)
    
    glfwDestroyWindow(window)
    glfwTerminate()
