
import glfw
import opengl
import boomslang/image
import "sprite.boomslang"
import "gamedata.boomslang"
import "scene.boomslang"

def main()
	unless glfwInit()
		die()
	
	GlfwWindow^ window = glfwCreateWindow(640, 480, "Simple Platformer Game", void, void)
	GameData gamedata
	
	if window = void
		"Failed to create window".output()
		die()
	
	# Initialize
	glfwMakeContextCurrent(window)
	glfwSwapInterval(1)
	glClearColor(0, 0.8, 0, 1)
	
	gamedata.resources.load()
	gamedata.load()
	
	until glfwWindowShouldClose(window)
		Integer width
		Integer height
		
		# Get Window Width and Height
		glfwGetFramebufferSize(window, &width, &height)
		
		# Clear Screen
		glClear(GL_COLOR_BUFFER_BIT)
		
		# Set View
		glViewport(0i, 0i, width, height)
		glMatrixMode(GL_PROJECTION)
		glLoadIdentity()
		glOrtho(0, width.toNumber(), height.toNumber(), 0, 1, -1)
		
		update(window, &gamedata)
		render(window, &gamedata)
		
		glfwPollEvents()
		glfwSwapBuffers(window)
	
	glfwDestroyWindow(window)
	glfwTerminate()
